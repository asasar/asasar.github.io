---
title: Building image of ubuntu 20.04 with Packer for Nutanix
author: asasar
date: 2023-01-10 03:33:00 +0800
categories: [Packer, Ubuntu, Nutanix]
tags: [Nutanix, Packer, Ubuntu]
pin: true
math: true
mermaid: true
image:
  path: /assets/img/packer.png
  alt: Building image of ubuntu 20.04 with Packer for Nutanix.
---

# Creating an Ubuntu 20 Image with Packer for Nutanix


## Requirements

The Url to the image Ubuntu 22.04 by default https://cloud-images.ubuntu.com/releases/22.04/release/ubuntu-22.04-server-cloudimg-amd64.img

The information required to connect to Nutanix

## Usage

To create an Ubuntu 22.04 image using Packer, follow these steps:

1. Make sure you have Packer installed on your system.
2. Change the default settings.auto.pkrvars.hcl 
3. Run the following command to create the image: `packer build .`.


```terraform

# plugin.pkr.hcl

packer {
  required_plugins {
    nutanix = {
      version = ">= 0.8.0"
      source = "github.com/nutanix-cloud-native/nutanix"
    }
  }
}

# source.pkr.hcl

source "nutanix" "ubuntu22" {
  nutanix_username = var.nutanix_username
  nutanix_password = var.nutanix_password
  nutanix_endpoint = var.nutanix_endpoint
  nutanix_port     = var.nutanix_port
  nutanix_insecure = var.nutanix_insecure
  cluster_name     = var.nutanix_cluster
  os_type          = "Linux"
  vm_disks {
    image_type       = "DISK_IMAGE"
    source_image_uri = var.source_image_uri
    disk_size_gb     = 40
  }
  vm_nics {
    subnet_name = var.nutanix_subnet
  }
  image_description = "Packer build Ubuntu server live 22.04 at ${timestamp()}"
  force_deregister  = true
  vm_force_delete   = true
  image_name        = var.image_name_output
  user_data         = base64encode(templatefile("cloud-init/cloud-config-ubuntu.tpl", { ssh_authorized_keys = var.ssh_authorized_keys }))
  shutdown_command  = "echo 'packer' | sudo -S shutdown -P now"
  shutdown_timeout  = "2m"
  ssh_password      = "packer"
  ssh_username      = "builder"
}

# build.pkr.hcl

build {
  source "nutanix.ubuntu22" {
    name = "ubuntu22"
  }
}

# variables.pkr.hcl
variable "nutanix_username" {
  type        = string
  description = "The username to use to authenticate with the Nutanix API"
}

variable "nutanix_password" {
  type        = string
  sensitive   = true
  description = "The password to use to authenticate with the Nutanix API"
}

variable "nutanix_endpoint" {
  type        = string
  description = "The URL of the Nutanix API endpoint"
}

variable "nutanix_port" {
  type        = number
  default     = 9440
  description = "The port of the Nutanix API endpoint"
}

variable "nutanix_insecure" {
  type        = bool
  description = "Whether to ignore SSL certificate errors"
}

variable "nutanix_subnet" {
  type        = string
  description = "The name of the subnet to deploy the VM into"
}

variable "nutanix_cluster" {
  type        = string
  description = "The name of the cluster to deploy the VM into"
}

variable "source_image_uri" {
  type        = string
  description = "The URI of the source image to use for the VM"
}

variable "ssh_authorized_keys" {
  type        = string
  default     = null
  sensitive   = true
  description = "The public SSH key to add to the VM"
}

variable "image_name_output" {
  type        = string
  description = "The name of the image generated by the Packer build"
}

```

For testing create the file settings.auto.pkrvars.hcl and completed with the values required for you cluster Nutanix.

```text
# settings.auto.pkrvars.hcl

nutanix_username    = "admin"
nutanix_password    = "XXX XXXXX"
nutanix_insecure    = false
nutanix_endpoint    = "10.0.0.1"
nutanix_port        = 9440
nutanix_cluster     = "Cluster Name"
nutanix_subnet      = "Primary"
source_image_uri    = "https://cloud-images.ubuntu.com/releases/22.04/release/ubuntu-22.04-server-cloudimg-amd64.img"
ssh_authorized_keys = "ssh Public SSH Key"
image_name_output   = "ubuntu-22-packer-nutanic"
```


Create the folder cloud-init and add file cloud-config-ubuntu.tpl

```yaml
#cloud-config
users:
  - name: builder
    groups:
      - users
      - admin
      - adm
      - wheel
      - root
      - sudo
    sudo: ["ALL=(ALL) NOPASSWD:ALL"]
    ssh-authorized-keys:
      - { ssh_authorized_keys }

chpasswd:
  list: |
    builder:packer
  expire: False
ssh_pwauth: True

```